ext {
  VERSION = project.findProperty('version')
  VCS_URL = 'https://github.com/comodal/pagerduty-client'
  jsoniter = "2.11.+" // https://github.com/comodal/json-iterator/releases
}

final JLV = JavaLanguageVersion.of(project.findProperty('javaVersion') as Integer ?: 24)

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'

  plugins.withType(JavaPlugin).configureEach {
    java {
      modularity.inferModulePath = true
      toolchain {
        languageVersion = JLV
      }
    }
  }

  project.group = 'systems.comodal'
  project.version = "$VERSION"

  repositories {
    maven {
      url = "https://maven.pkg.github.com/comodal/json-iterator"
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
        password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
      }
    }
    mavenCentral()
  }

  dependencies {
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  }

  test {
    useJUnitPlatform()
    testLogging {
      events "skipped", "failed", "standardOut", "standardError"
      exceptionFormat = "full"
      showStandardStreams = true
    }
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

  javadoc {
    options.addBooleanOption('html5', true)
    options.addStringOption('Xdoclint:none', '-quiet')
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        groupId = project.group
        artifactId = project.name
        version = project.version

        pom {
          name = project.name
          description = "PagerDuty Event Client"
          url = "$VCS_URL"
          licenses {
            license {
              name = 'Apache License 2.0'
              url = 'https://github.com/comodal/pagerduty-client/blob/master/LICENSE'
            }
          }
          developers {
            developer {
              id = 'jpe42'
              organization = 'Comodal Systems'
              organizationUrl = 'https://github.com/comodal'
            }
          }
          scm {
            connection = 'scm:git:git@github.com:comodal/pagerduty-client.git'
            developerConnection = 'scm:git:ssh@github.com:comodal/pagerduty-client.git'
            url = "$VCS_URL"
          }
        }
      }
    }

    repositories {
      maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/comodal/pagerduty-client"
        credentials {
          username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user.write")
          password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token.write")
        }
      }
    }
  }
}
