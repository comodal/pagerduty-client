buildscript {
  def getAppVersion = { ->
    try (final var gitTagOut = new ByteArrayOutputStream()) {
      exec {
        commandLine 'git', 'tag', '--points-at', 'HEAD'
        standardOutput = gitTagOut
      }
      final var tagName = gitTagOut.toString().strip()
      if (tagName.isBlank()) {
        try (final var gitHashOut = new ByteArrayOutputStream()) {
          exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = gitHashOut
          }
          return 'git-' + gitHashOut.toString().strip()
        }
      } else {
        return tagName
      }
    }
  }

  ext {
    VERSION = getAppVersion()
    VCS_URL = 'https://github.com/comodal/pagerduty-client'
    // Used by systems.comodal.pagerduty_event_json_iterator_adapter
    jsoniter = "2.11.+" // https://github.com/comodal/json-iterator/releases
  }
}

final JLV = JavaLanguageVersion.of(project.findProperty('targetJava') as Integer ?: 22)

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'

  plugins.withType(JavaPlugin).configureEach {
    java {
      modularity.inferModulePath = true
      toolchain {
        languageVersion = JLV
      }
    }
  }

  project.group = 'systems.comodal'

  repositories {
    maven {
      url = "https://maven.pkg.github.com/comodal/json-iterator"
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
        password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
      }
    }
    mavenCentral()
  }

  configurations.configureEach {
    resolutionStrategy {
      cacheDynamicVersionsFor 15, 'minutes'
      cacheChangingModulesFor 15, 'minutes'
    }
  }

  dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:+'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:+'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:+'
  }

  test {
    useJUnitPlatform()
    maxParallelForks = 4
    testLogging {
      events "passed", "skipped", "failed", "standardOut", "standardError"
      exceptionFormat "full"
      showStandardStreams true
    }
  }

  tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
  }

  tasks.register('javadocJar', Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
  }

  javadoc {
    options.addBooleanOption('html5', true)
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        artifact sourcesJar
        artifact javadocJar
        groupId project.group
        artifactId project.name
        version = "$VERSION"
        pom {
          name = project.name
          url = "$VCS_URL"
          licenses {
            license {
              name = 'Apache License 2.0'
              url = 'https://github.com/comodal/pagerduty-client/blob/master/LICENSE'
            }
          }
          scm {
            connection = 'scm:git:git@github.com:comodal/pagerduty-client.git'
            url = "$VCS_URL"
          }
        }
      }
    }
    repositories {
      maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/comodal/pagerduty-client"
        credentials {
          username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user.write")
          password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token.write")
        }
      }
    }
  }
}
