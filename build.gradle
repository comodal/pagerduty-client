buildscript {
  configurations.all {
    resolutionStrategy {
      cacheDynamicVersionsFor 180, 'minutes'
      cacheChangingModulesFor 180, 'minutes'
    }
  }
  repositories {
    maven { url = 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath "org.javamodularity:moduleplugin:+"
    classpath "pl.allegro.tech.build:axion-release-plugin:+"
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:+"
  }
}

ext {
  // Used by systems.comodal.pagerduty_event_json_iterator_adapter
  jsoniter = "2.3.+" // https://github.com/comodal/json-iterator/releases
}

apply plugin: 'pl.allegro.tech.build.axion-release'

scmVersion {
  tag.prefix = ''
  repository.pushTagsOnly = true
}

// https://docs.gradle.org/5.6.2/userguide/building_java_projects.html#sec:java_cross_compilation
def javaHomePath = new File("${project.findProperty('javaHome') ?: System.getenv('JAVA_HOME')}")
def javaExecutables = [:].withDefault { execName ->
  def executable = new File(javaHomePath, "bin/${execName}")
  assert executable.exists(): "There is no ${execName} executable in ${javaHomePath}/bin"
  executable
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'org.javamodularity.moduleplugin'
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.bintray'

  tasks.withType(AbstractCompile) {
    options.with {
      fork = true
      forkOptions.javaHome = javaHomePath
    }
  }
  tasks.withType(Javadoc) {
    executable = javaExecutables.javadoc
  }
  tasks.withType(Test) {
    executable = javaExecutables.java
  }
  tasks.withType(JavaExec) {
    executable = javaExecutables.java
  }

  project.group = 'systems.comodal'
  project.version = scmVersion.version

  sourceCompatibility = JavaVersion.toVersion(project.findProperty('targetJava') ?: JavaVersion.VERSION_14)

  ext {
    desc = 'Pagerduty Event Client'
    bintrayOrg = 'comodal'
    bintrayRepo = 'libraries'
    vcsUrl = 'https://github.com/comodal/pagerduty-client'
  }

  repositories {
    maven { url = 'https://dl.bintray.com/comodal/libraries' }
    jcenter()
    mavenCentral()
  }

  configurations.all {
    resolutionStrategy {
      cacheDynamicVersionsFor 15, 'minutes'
      cacheChangingModulesFor 15, 'minutes'
    }
  }

  dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:+'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:+'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:+'
  }

  test {
    useJUnitPlatform()
    maxParallelForks = 4
    testLogging {
      events "passed", "skipped", "failed", "standardOut", "standardError"
      exceptionFormat "full"
      showStandardStreams true
    }
    moduleOptions {
      runOnClasspath = true
    }
  }

  task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
  }

  task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
  }

  javadoc {
    options.addBooleanOption('html5', true)
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        artifact sourcesJar
        artifact javadocJar
        groupId project.group
        artifactId project.name
        version = project.version
        pom {
          name = project.name
          description = project.desc
          url = project.vcsUrl
          licenses {
            license {
              name = 'Apache License 2.0'
              url = 'https://github.com/comodal/pagerduty-client/blob/master/LICENSE'
            }
          }
          developers {
            developer {
              email = 'james.p.edwards42@gmail.com'
            }
          }
          scm {
            connection = 'scm:git:git@github.com:comodal/pagerduty-client.git'
            url = project.vcsUrl
          }
        }
      }
    }
  }

  afterEvaluate {
    bintray {
      override = true
      user = project.hasProperty("bintrayUser") ? bintrayUser : System.getenv('BINTRAY_USER')
      key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : System.getenv('BINTRAY_API_KEY')
      publications = ['mavenJava']
      pkg {
        userOrg = project.bintrayOrg
        repo = project.bintrayRepo
        name = project.name
        desc = project.desc
        vcsUrl = project.vcsUrl
        websiteUrl = project.vcsUrl
        issueTrackerUrl = project.vcsUrl + '/issues'
        licenses = ["Apache-2.0"]
        publish = true
        version {
          name = project.version
          vcsTag = project.name + '-' + project.version
          released = new Date()
          gpg {
            sign = true
            passphrase = project.hasProperty("bintrayGPGPassphrase") ? bintrayGPGPassphrase : System.getenv('BINTRAY_GPG_PASSPHRASE')
          }
        }
      }
    }
  }
}
